
%skeleton "lalr1.cc"
%defines
%define api.namespace {rxml}

%locations
%error-verbose
/* %debug */

%code requires 
{

class XmlLexer;

#include <map>
#include <string>

#include "SaxParser.h"
}

%parse-param {XmlLexer& lex}
%parse-param {SaxParser& sax}
%parse-param {std::ostream& err}
%parse-param {std::string filename}
%lex-param   {XmlLexer& lex}

%initial-action
{
    // Initialize the initial location.
    @$.begin.line = @$.end.line = 1;
    @$.begin.filename = @$.end.filename = &filename;
};

// Symbols.
%union
{    
    std::string*                         string;
    std::map<std::string, std::string>*  string_map;
    std::pair<std::string, std::string>* string_pair;
}

%token              END 0           "end of file"
%token              ERROR           "lexing error"

%token <string>     WS              "white space"
%token <string>     NL              "new line"
%token <string>     COMMENT         "comment"
%token              LT              "<"
%token              LTS             "</"
%token              GT              ">"
%token              GTS             "/>"
%token              PIO             "<?"
%token              PIE             "?>"
%token              EQUAL           "="
%token <string>     TEXT            "character data"
%token <string>     NAME            "name"
%token <string>     STRING          "string"
%token <string>     CDATA           "cdata section"

%type <string_pair> attribute
%type <string_map>  attributes

%destructor { delete $$; } <string>
%destructor { delete $$; } <string_map>
%destructor { delete $$; } <string_pair>

%{
int yylex(rxml::parser::semantic_type* yylval, rxml::parser::location_type* yylloc, XmlLexer& lex);

#include "XmlLexer.h"
#include <memory>

template <typename Type>
std::shared_ptr<Type> wrap(Type* type)
{
    return std::shared_ptr<Type>(type);
}
%}

%%

document:
  prolog misc element misc;

prolog:
  pi | /* nothing */;
  
misc: 
    comment | pi | s | /* nothing */;

pi:
  "<?" NAME s attributes s "?>" {
    // TODO     
    delete $2;
    delete $4;
  };  
  
element:
  stag content etag | emptytag;
  
emptytag:
  "<" NAME s attributes s "/>" {
    if (sax.start_element_cb)
    {
        sax.start_element_cb(*$2, *$4);
    }
    if (sax.end_element_cb)
    {
        sax.end_element_cb(*$2);
    }
    delete $2;
    delete $4;
  };
  
stag:
  "<" NAME s attributes s ">" {
    if (sax.start_element_cb)
    {
        sax.start_element_cb(*$2, *$4);
    }
    delete $2;
    delete $4;
  };
  
etag:
  "</" NAME s ">" {
    if (sax.end_element_cb)
    {
        sax.end_element_cb(*$2);
    }
    delete $2;
  };
  
attributes:
  attributes attribute {
    $$ = $1;
    // TODO fail if the attribute is double defined
    $$->insert(*$2);
    delete $2;
  } |
  /* nothing */ {
    $$ = new std::map<std::string, std::string>();
  };
  
attribute:
  NAME "=" STRING s {
    $$ = new std::pair<std::string, std::string>(*$1, *$3);
    delete $1;
    delete $3;
  };

content:
    content nodes |
    /* nothing */;

nodes:
  text | 
  element | 
  pi |
  cdata |
  comment; 
  
text: 
  TEXT {
    if (sax.text_cb)
    {
        sax.text_cb(*$1);
    }
    delete $1;
  } | 
  WS {
    if (sax.text_cb)
    {
        sax.text_cb(*$1);
    }
    delete $1;
  };

cdata:
  CDATA {
    if (sax.cdata_cb)
    {
        sax.cdata_cb(*$1);
    }
    delete $1;
  };

comment:
  COMMENT;

s:
  WS | NL | /* nothing */;
  
%%

#include <iostream>

#include "XmlLexer.h"

void rxml::parser::error(const location_type& l, const std::string& m)
{    
    err << *l.begin.filename << "(" << l.begin.line << "): " << m << std::endl;
}

int yylex(rxml::parser::semantic_type* yylval, rxml::parser::location_type* yylloc, XmlLexer& lex)
{
    return lex.lex(yylval, yylloc);
}
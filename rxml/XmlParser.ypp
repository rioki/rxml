/*
// rxml - rioki's xml lbrary
// Copyright 2016-2018 Sean "rioki" Farrell <sean.farrell@rioki.org>
// 
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
// 
// The above copyright notice and this permission notice shall be included in
// all copies or substantial portions of the Software.
// 
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
// THE SOFTWARE.
*/

%skeleton "lalr1.cc"
%defines
%define api.namespace {rxml}

%locations
%error-verbose
/* %debug */

%code requires 
{

class XmlLexer;

#include <map>
#include <string>

#include "SaxParser.h"
}

%parse-param {XmlLexer& lex}
%parse-param {SaxParser& sax}
%parse-param {std::ostream& err}
%parse-param {std::string filename}
%lex-param   {XmlLexer& lex}

%initial-action
{
    // Initialize the initial location.
    @$.begin.line = @$.end.line = 1;
    @$.begin.filename = @$.end.filename = &filename;
};

// Symbols.
%union
{    
    std::string*                         string;
    std::map<std::string, std::string>*  string_map;
    std::pair<std::string, std::string>* string_pair;
}

%token              END 0           "end of file"
%token              ERROR           "lexing error"

%token <string>     WS              "white space"
%token <string>     NL              "new line"
%token <string>     COMMENT         "comment"
%token              LT              "<"
%token              LTS             "</"
%token              GT              ">"
%token              GTS             "/>"
%token              PIO             "<?"
%token              PIE             "?>"
%token              EQUAL           "="
%token <string>     TEXT            "character data"
%token <string>     NAME            "name"
%token <string>     STRING          "string"
%token <string>     CDATA           "cdata section"

%type <string_pair> attribute
%type <string_map>  attributes attributes_op

/*
%destructor { delete $$; } <string>
%destructor { delete $$; } <string_map>
%destructor { delete $$; } <string_pair>
*/

%{
int yylex(rxml::parser::semantic_type* yylval, rxml::parser::location_type* yylloc, XmlLexer& lex);

#include "XmlLexer.h"
#include <memory>

template <typename Type>
std::shared_ptr<Type> wrap(Type* type)
{
    return std::shared_ptr<Type>(type);
}
%}

%%

document:
  misc element misc;
  
misc: 
    misc misc_elem |
    /* nothing */;

misc_elem:
    comment | pi | s;

pi:
  "<?" NAME s attributes s_op "?>" {
    // TODO     
    delete $2;
    delete $4;
  };  
  
element:
  stag content etag | emptytag;
  
emptytag:
  "<" NAME attributes_op "/>" {
    if (sax.start_element_cb)
    {
        sax.start_element_cb(*$2, *$3);
    }
    if (sax.end_element_cb)
    {
        sax.end_element_cb(*$2);
    }
    delete $2;
    delete $3;
  };
  
stag:
  "<" NAME attributes_op ">" {
    if (sax.start_element_cb)
    {
        sax.start_element_cb(*$2, *$3);
    }
    delete $2;
    delete $3;
  };
  
etag:
  "</" NAME s_op ">" {
    if (sax.end_element_cb)
    {
        sax.end_element_cb(*$2);
    }
    delete $2;
  };
  
attributes_op:
  s attributes s_op {
    $$ = $2;
  } |
  s_op {
    $$ = new std::map<std::string, std::string>();
  };
  
attributes:
  attributes s attribute {
    $$ = $1;
    // TODO fail if the attribute is double defined
    $$->insert(*$3);
    delete $3;
  } |
  attribute {
    $$ = new std::map<std::string, std::string>();
    $$->insert(*$1);
    delete $1;
  };
  
attribute:
  NAME "=" STRING {
    $$ = new std::pair<std::string, std::string>(*$1, *$3);
    delete $1;
    delete $3;
  };

content:
    content nodes |
    /* nothing */;

nodes:
  text | 
  element | 
  pi |
  cdata |
  comment; 
  
text: 
  TEXT {
    if (sax.text_cb)
    {
        sax.text_cb(*$1);
    }
    delete $1;
  } | 
  WS {
    if (sax.text_cb)
    {
        sax.text_cb(*$1);
    }
    delete $1;
  };

cdata:
  CDATA {
    if (sax.cdata_cb)
    {
        sax.cdata_cb(*$1);
    }
    delete $1;
  };

comment:
  COMMENT {
    if (sax.cdata_cb)
    {
        sax.comment_cb(*$1);
    }
    delete $1;
  };

s:
  WS | NL;

s_op:
  s | /* nothing */;
  
%%

#include <iostream>

#include "XmlLexer.h"

void rxml::parser::error(const location_type& l, const std::string& m)
{    
    err << *l.begin.filename << "(" << l.begin.line << "): " << m << std::endl;
}

int yylex(rxml::parser::semantic_type* yylval, rxml::parser::location_type* yylloc, XmlLexer& lex)
{
    return lex.lex(yylval, yylloc);
}